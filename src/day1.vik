import "../../../src/libvik/prelude.vik"
import File, FileResult in "../../../src/libvik/file.vik"
import u32_parse, ensure_trailing_char in "common.vik"

const MAX_DIAL: i32 = 100

namespace Pt2 {
    fn norm(x: i32, by: i32): i32 {
        let y = x % by
        if (y < 0) return y + by
        return y
    }

    fn turn_once(state: i32, by: i32, record: &uint): i32 {
        let wrt = state + by
        let nstate = norm(wrt, MAX_DIAL)
        let n = wrt / MAX_DIAL
        if by < 0 {
            if (state == 0) n += 1
            if (wrt <= 0) n = -n + 1
        }
        *record += n as uint    
        return nstate
    }

}

namespace Pt1 {
    fn turn_once(state: i32, by: i32, record: &uint): i32 {
        let nstate = (state + by) % MAX_DIAL
        if (nstate == 0) 
            *record += 1
        return nstate
    }

}

fn dispatch_turn_once(part: uint, state: i32, by: i32, record: &uint): i32 {
    when part {
        1 -> return Pt1.turn_once(state, by, record)
        2 -> return Pt2.turn_once(state, by, record)
        else -> assert(false, "unreachable")
    }
    return 0
}

fn solve(part: uint, file: [u8]) {
    file = ensure_trailing_char(file, '\n')
    let dir: i32 = 0, state: i32 = 50, num_start = 0, answer = 0
    for i in 0..file.len() {
        when file[i] {
            '\n' -> {
                let num = u32_parse(&file[num_start], i - num_start) as i32
                state = dispatch_turn_once(part, state, dir * num,  &answer)
            }
            'L' -> {
                dir = -1
                num_start = i + 1
            }
            'R' -> {
                dir = 1
                num_start = i + 1
            }
            else -> { }
        }
    }
    println("dial was at zero %d times", answer)
}

fn main(): i32 {
    let file_path = str.from_cstr("out.txt")
    when File.read_all(&file_path) {
        FileOpenError | FileReadError -> {
            println("error reading file %.*s", file_path.len(), file_path.raw())
        }
        FileOpenSuccess(contents) -> {
            solve(2, contents)
            return 0
        }
    }
}